plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.mikepenz.aboutlibraries.plugin'
}

android {
    namespace 'com.cyb3rko.pazzword'

    compileSdk 35
    defaultConfig {
        applicationId "com.cyb3rko.pazzword"
        minSdk 21
        targetSdk 35
        versionCode 12
        versionName '1.2.4'
        resourceConfigurations += ['en', 'de']
        signingConfig signingConfigs.debug
    }
    buildTypes {
        release {
            minifyEnabled true
            crunchPngs true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
}

aboutLibraries {
    excludeFields = ['generated']
}

if (project.hasProperty('sign')) {
    android {
        signingConfigs {
            release {
                storeFile file(System.getenv('KEYSTORE_FILE'))
                storePassword System.getenv('KEYSTORE_PASSWD')
                keyAlias System.getenv('KEYSTORE_KEY_ALIAS')
                keyPassword System.getenv('KEYSTORE_KEY_PASSWD')
            }
        }
    }
    android.buildTypes.release.signingConfig android.signingConfigs.release
}

dependencies {
    def navigation_version = "2.8.3"

    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
    implementation "com.mikepenz:aboutlibraries-core:$aboutLibsVersion"
    implementation "com.mikepenz:aboutlibraries:$aboutLibsVersion"
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'com.airbnb.android:lottie:6.5.2'
    implementation 'com.github.cyb3rko:about-icons:2.0.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'io.github.medyo:android-about-page:2.0.0'
    implementation 'me.gosimple:nbvcxz:1.5.1'
}